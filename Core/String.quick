struct String
{
    data *var Char;
    count Int;
    capacity Int;

    func Create() String
    {
        val defaultCapacity = 16;

        var string = String
        {
            data = Internal.malloc((defaultCapacity * sizeof Char) as UInt) as *var Char,
            count = 0,
            capacity = defaultCapacity,
        };

        string.NullTerminate();

        return string;
    }

    func Destroy(me *var String) Void
    {
        me.count = 0;
        me.capacity = 0;

        Internal.free(me.data as *var Void);
    }

    func FromSpan(span Span.<Char>) String
    {
        var string = String.Create();
        string.PushSpan(span);

        return string;
    }

    func ToSpan(me *val String) Span.<Char>
    {
        return Span.<Char>
        {
            data = me.data,
            count = me.count,
        };
    }

    func Reserve(me *var String, count Int) Void
    {
        val newTargetCapacity = me.count + count;

        if (newTargetCapacity <= me.capacity)
        {
            return;
        }

        while (me.capacity < newTargetCapacity)
        {
            me.capacity *= 2;
        }

        me.data = Internal.realloc(me.data as *var Void, (me.capacity * sizeof Char) as UInt) as *var Char;
    }

    func NullTerminate(me *var String) *var String
    {
        me.Reserve(1);
        var stringChar = (me.data as UInt + me.count as UInt) as *var Char;
        stringChar.* = '\0';

        return me;
    }

    func ToString(me *var String, string *var String) Void
    {
        string.PushCString(me.data);
    }

    func Push.<T>(me *var String, value T) *var String
    {
        value.ToString(me);

        return me;
    }

    func PushSpan(me *var String, span Span.<Char>) *var String
    {
        me.Reserve(span.count);

        for (val i of 0 < span.count)
        {
            var stringChar = (me.data as UInt + me.count as UInt) as *var Char;
            var spanChar = (span.data as UInt + i as UInt) as *var Char;
            stringChar.* = spanChar.*;
            me.count += 1;
        }

        me.NullTerminate();

        return me;
    }

    func PushCString(me *var String, cString *val Char) *var String
    {
        if (cString == 0 as *val Char)
        {
            return me;
        }

        while (cString.* != '\0')
        {
            me.Push.<Char>(cString.*);
            cString = (cString as UInt + 1) as *val Char;
        }

        me.NullTerminate();

        return me;
    }

    func PushPointer.<T>(me *var String, value *val T) *var String
    {
        const bufferCount = 32;
        var buffer [bufferCount]Char = ?;

        Internal.CoreSystemPointerToString(buffer as *var Char, bufferCount, value as *val Void);
        me.PushCString(buffer as *val Char);

        return me;
    }
}